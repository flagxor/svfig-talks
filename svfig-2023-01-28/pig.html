<!DOCTYPE html>
<script src="../websent/websent.js"></script><script>
FONT = 'consolas; courier';
USABLE = 0.9;
LINE_SPACING = 1.2;
BACKGROUND = '#cc4444';
FOREGROUND = '#ffdd00';
</script><pre>

   Pig Latin
   CHALLENGE
      ğŸ–
January 28, 2023

PIG LATIN
ğŸ· Argot or jargon
ğŸ¶ Dog Latin
    Costard: Go to; thou hast it ad dungill,
             at the fingers' ends, as they say.
    Holofernes: O, I smell false Latine;
                dunghill for unguem.
      â€”â€‰Love's Labour's Lost, William Shakespeare
ğŸ· Modern version appears ~1919.
ğŸ· bsdgames - /usr/games/pig

SINGLE  CONSONANT
    ğŸ– ğŸ– ğŸ–
   pig â†¬ igpay
 latin â†¬ atinlay
banana â†¬ ananabay
 happy â†¬ appyhay
  duck â†¬ uckday
    me â†¬ emay
   too â†¬ ootay
  will â†¬ illway
  real â†¬ ealray
simple â†¬ implesay
   say â†¬ aysay
 bagel â†¬ agelbay

    SINGLE  CONSONANT
        ğŸ– ğŸ– ğŸ–
p - ig    â†¬ ig    - p - ay
l - atin  â†¬ atin  - l - ay
b - anana â†¬ anana - b - ay
h - appy  â†¬ appy  - h - ay
d - uck   â†¬ uck   - d - ay
m - e     â†¬ e     - m - ay
t - oo    â†¬ oo    - t - ay
w - ill   â†¬ ill   - w - ay
r - eal   â†¬ eal   - r - ay
s - imple â†¬ imple - s - ay
s - ay    â†¬ ay    - s - ay
b - agel  â†¬ agel  - b - ay

  MULTI-CONSONANT
     ğŸ– ğŸ– ğŸ–
friends â†¬ iendsfray
  smile â†¬ ilesmay
 string â†¬ ingstray
 stupid â†¬ upidstay
  glove â†¬ oveglay
  trash â†¬ ashtray
  floor â†¬ oorflay
  store â†¬ orestay

       MULTI-CONSONANT
          ğŸ– ğŸ– ğŸ–
fr  - iends â†¬ iends - fr  - ay
sm  - ile   â†¬ ile   - sm  - ay
str - ing   â†¬ ing   - str - ay
st  - upid  â†¬ upid  - st  - ay
gl  - ove   â†¬ ove   - gl  - ay
tr  - ash   â†¬ ash   - tr  - ay
fl  - oor   â†¬ oor   - fl  - ay
st  - ore   â†¬ ore   - st  - ay

   INITIAL VOWEL
     ğŸ– ğŸ– ğŸ–
    eat â†¬ eatway
 omelet â†¬ omeletway
    are â†¬ areway
    egg â†¬ eggway
explain â†¬ explainway
 always â†¬ alwaysway
   ends â†¬ endsway
 honest â†¬ honestway
      I â†¬ Iway

     INITIAL VOWEL
       ğŸ– ğŸ– ğŸ–
    eat â†¬ eat     - way
 omelet â†¬ omelet  - way
    are â†¬ are     - way
    egg â†¬ egg     - way
explain â†¬ explain - way
 always â†¬ always  - way
   ends â†¬ ends    - way
 honest â†¬ honest  - way
      I â†¬ I       - way

     INITIAL VOWEL
       ğŸ– ğŸ– ğŸ–
    eat â†¬ eat     - way
 omelet â†¬ omelet  - way
    are â†¬ are     - way
    egg â†¬ egg     - way
explain â†¬ explain - way
 always â†¬ always  - way
   ends â†¬ ends    - way
 honest â†¬ onest - h -ay
      I â†¬ I       - WAY

        QU-
     ğŸ– ğŸ– ğŸ–
   quest â†¬ estquay
quantity â†¬ antityquay

            QU-
         ğŸ– ğŸ– ğŸ–
qu - est    â†¬ est    - qu - ay
qu - antity â†¬ antity - qu - ay

          Y-
       ğŸ– ğŸ– ğŸ–
      yes â†¬ yesay
yesterday â†¬ yesterdayay
     yell â†¬ yellay

          Y-
       ğŸ– ğŸ– ğŸ–
      yes â†¬ yes       - ay
yesterday â†¬ yesterday - ay
     yell â†¬ yell      - ay

ENCODING APPROACH
ğŸ· Keep a buffer for first constant cluster
ğŸ· Fill character by character
ğŸ· Once a non-consonant is seen, consider it "stemmed"
ğŸ· Print first vowel in case based on the "stem"
ğŸ· At the end emit buffer in lowercase + final -way/-ay

ENCODING APPROACH
    ğŸ– ğŸ– ğŸ–
 Scram
|
 ğŸ–         :buffer
 ğŸ–         :output

ENCODING APPROACH
    ğŸ– ğŸ– ğŸ–
 Scram
 |
 SğŸ–        :buffer
 ğŸ–         :output

ENCODING APPROACH
    ğŸ– ğŸ– ğŸ–
 Scram
  |
 ScğŸ–       :buffer
 ğŸ–         :output

ENCODING APPROACH
    ğŸ– ğŸ– ğŸ–
 Scram
   |
 ScrğŸ–      :buffer
 ğŸ–         :output

ENCODING APPROACH
    ğŸ– ğŸ– ğŸ–
 Scram
    |
 ScrğŸ–      :buffer
 AğŸ–        :output

ENCODING APPROACH
    ğŸ– ğŸ– ğŸ–
 Scram
     |
 ScrğŸ–      :buffer
 AmğŸ–       :output

ENCODING APPROACH
    ğŸ– ğŸ– ğŸ–
 Scram
      |
 ScrğŸ–      :buffer
 AmscrğŸ–    :output

ENCODING APPROACH
    ğŸ– ğŸ– ğŸ–
 Scram
      |
 ScrğŸ–      :buffer
 AmscrayğŸ–  :output

 BASICS
ğŸ– ğŸ– ğŸ–
: between? ( ch a b ) >r over r> <= >r >= r> and ;
: upper? ( ch -- f ) [char] A [char] Z between? ;
: lower? ( ch -- f ) [char] a [char] z between? ;
: letter? ( ch -- f ) dup upper? swap lower? or ;
: cupper ( ch -- ch )
   dup lower? if [char] a - [char] A + then ;
: clower ( ch -- ch )
   dup upper? if [char] A - [char] a + then ;

VOWELS AND CONSONANTS
      ğŸ– ğŸ– ğŸ–
: contains? ( ch a n -- f ) 
   1- for
     dup c@ >r over r> = if
       rdrop 2drop -1 exit
     then
     1+
   next
   2drop 0 ;
: vowely? ( ch -- f )
   clower s" aeiouy" contains? ;
: consonant? ( ch -- f )
   clower s" bcdfghjklmnpqrstvwxz" contains? ;

KEEP A BUFFER
  ğŸ– ğŸ– ğŸ–
1000 constant capacity
create buffer capacity allot
create alt-buffer capacity allot
variable slot  buffer slot !

OPERATIONS ON BUFFER
     ğŸ– ğŸ– ğŸ–
: clear   buffer slot ! ;
: +buffer ( ch -- ) slot @ c!  1 slot +! ;
: full? ( -- f ) buffer slot @ <> ;
: buffer> ( -- a n ) buffer slot @ buffer - ;

PRINT BUFFER IN LOWERCASE
       ğŸ– ğŸ– ğŸ–
: ltype ( a n -- )
   for aft
     dup c@ clower emit 1+
   then next
   drop ;
: buffer.   buffer> ltype ;

TRACK FIRST STEM
   ğŸ– ğŸ– ğŸ–
variable stemmed
: capital? ( -- f )
   full? buffer c@ upper? and ;
: capvowel ( ch -- ch )
   stemmed @ 0= capital? and if cupper then ;
: +consonant ( ch -- )
   stemmed @ if emit else +buffer then ;

END OF A WORD
  ğŸ– ğŸ– ğŸ–
: finish
   full? if
     buffer. ." ay" clear
   else
     stemmed @ if ." way" then
   then
   0 stemmed ! ;

PUT IT TOGETHER
   ğŸ– ğŸ– ğŸ–
: pig1 ( ch -- )
   dup vowely? if
     capvowel emit -1 stemmed ! exit
   then
   dup consonant? if
     +consonant exit
   then
   finish emit ;
: pig   begin key dup 0< if bye then pig1 again ;

What about
  in reverse?

pork â†¬ orkpay
 ğŸ– ğŸ– ğŸ–
orkpay â†¬ pork

work â†¬ orkway
 are â†¬ areway
 ğŸ– ğŸ– ğŸ–
orkway â†¬ ork ?
areway â†¬ ware ?

play â†¬ ayplay
 ğŸ– ğŸ– ğŸ–
ayplay â†¬ layp ?
       â†¬ play ?

ONLY SOME STEMS IN ENGLISH
       ğŸ– ğŸ– ğŸ–
Single Letters
    AND
bl br ch chl chr cl cr cs cz dh dj dr dw fj fl fr
gh gl gn gr kh kl kn kr ks ll ls mn ph phl phr pl
pn pr ps pt qu rh sc sch schl schm schn schr scht
schw scl scr sh shl shr sht sk sl sm sn sp sph spl
spr squ sr st str sv sw th thr thw tr ts tw tz wh
wr zw

: clusters: begin create latestxt >name nip 5 > until ;
vocabulary clusters sealed  also clusters definitions
clusters: bl br ch chl chr cl cr cs cz dh dj dr dw fj fl fr gh gl gn gr
          kh kl kn kr ks ll ls mn ph phl phr pl pn pr ps pt qu rh
          sc sch schl schm schn schr scht schw scl scr sh shl shr sht
          sk sl sm sn sp sph spl spr squ sr st str sv sw th thr thw tr
          ts tw tz wh wr zw xxxxxxxx
only forth definitions
: cluster? ( a n -- f )
   dup 1 = >r over c@ consonant? r> and if 2drop -1 exit then
   clusters find forth ;

chops â†¬ opschay
   ğŸ– ğŸ– ğŸ–
opschay â†¬ hopsc
        â†¬ chops
        â†¬ schop

Check the
 Dictionary!

LOAD DICTIONARY IN MEMORY
      ğŸ– ğŸ– ğŸ–
s" /usr/share/dict/words" r/o open-file throw value dict-words
dict-words file-size throw constant dict-size
dict-size 1+ allocate throw value dict-data
dict-data dict-size dict-words read-file throw drop
0 dict-data dict-size + c!  ( null terminate )

ITERATE THROUGH EACH WORD
       ğŸ– ğŸ– ğŸ–
: next-word ( a -- a ) begin 1+ dup c@ nl = until 1+ ;
: each-step ( a -- a' a n ) dup next-word swap over over - 1- ;
: each{   postpone begin postpone dup postpone c@
          postpone while postpone each-step ; immediate
: }each   postpone repeat postpone drop ; immediate

COMPARE DISREGARDING CASE
       ğŸ– ğŸ– ğŸ–
: case= ( a n a n -- f )
   >r swap r@ <> if rdrop 2drop 0 exit then r>
   for aft
     2dup c@ clower swap c@ clower <> if rdrop 2drop 0 exit then
     1+ swap 1+ swap
   then next 2drop -1 ;

CHECK IF BUFFER HAS A WORD
       ğŸ– ğŸ– ğŸ–
: word? ( -- f )
   dict-data each{
     buffer> case= if drop -1 exit then
   }each 0 ; 
: word-w? ( -- f )
   dict-data each{
     buffer> 1- 0 max case= if drop -1 exit then
   }each 0 ; 

heat â†¬ eathay
   ğŸ– ğŸ– ğŸ–
eathay â†¬ heat
       â†¬ thea ( Thea)

CHECK IF BUFFER HAS A NONPROPER NOUN
          ğŸ– ğŸ– ğŸ–
: ?nonproper? ( a n -- a n f ) over c@ lower? ;
: nonpropers{
   postpone each{ postpone ?nonproper? postpone if ; immediate
: }nonpropers
   postpone else postpone 2drop postpone then
   postpone }each ; immediate

CHECK IF BUFFER HAS A NONPROPER NOUN
          ğŸ– ğŸ– ğŸ–
: nonproper? ( -- f )
   dict-data nonpropers{
     buffer> case= if drop -1 exit then
   }nonpropers 0 ;
: nonproper-w? ( -- f )
   dict-data nonpropers{
     buffer> 1- 0 max case= if drop -1 exit then
   }nonpropers 0 ;

MORE OPERATIONS ON BUFFER
       ğŸ– ğŸ– ğŸ–
: drop-buffer   slot @ 1- buffer max slot ! ;
: 1rot-buffer   buffer> buffer 1+ swap cmove> buffer> + c@ buffer c! ;
: rot-buffer ( n -- ) for aft 1rot-buffer then next ;
: stash-buffer   buffer> alt-buffer swap cmove ;
: unstash-buffer   buffer> >r alt-buffer swap r> cmove ;

UTILITIES
ğŸ– ğŸ– ğŸ–
: capupper.
   buffer c@ cupper emit  shift-buffer ;
: w? ( -- f )
   full? buffer buffer> + 1- max c@ [char] w = and ;

TRY A ROTATION
  ğŸ– ğŸ– ğŸ–
: lastn ( n -- a n )
   buffer> nip min >r buffer> r> dup >r - + r> ;
: try-rot ( xt n -- f )
   stash-buffer rot-buffer
   execute dup 0= if unstash-buffer then ;
: try-cluster-rot ( xt n -- f )
   dup lastn cluster? if try-rot else 2drop 0 then ;

TRY SEVERAL ROTATIONS
      ğŸ– ğŸ– ğŸ–
: try-rots ( -- f )
   2 4 do
     ['] word? i try-rot if unloop -1 exit then
   -1 +loop 0 ;
: try-clusters-rot { xt -- f }
   1 4 do
     xt i try-cluster-rot if unloop -1 exit then
   -1 +loop 0 ;

CONSIDER EACH IN ORDER
      ğŸ– ğŸ– ğŸ–
: rot-stem
   w? if nonproper-w? if drop-buffer exit then then
   ['] nonproper? try-clusters-rot if exit then
   w? if word-w? if drop-buffer exit then then
   ['] word? try-clusters-rot if exit then
   try-rots if exit then
   w? if drop-buffer else 1 rot-buffer then ;

PUT IT ALL TOGETHER
     ğŸ– ğŸ– ğŸ–
: unvent
   full? if
     capital? >r drop-buffer drop-buffer rot-stem
              r> if capupper.
   then buffer. clear then ;
: unpig1 ( ch -- )
   dup letter? if +buffer exit then unvent emit ;
: unpig
   begin key dup 0< if bye then unpig1 again ;

TWO MODES IN ONE PROGRAM
       ğŸ– ğŸ– ğŸ–
: usage   ." USAGE: pig.fs [-r]" cr 1 terminate ;
: run
   argc 3 > if usage then
   argc 3 = if 2 argv s" -r" str= 0= if usage then unpig exit then
   pig
;
run

SIZE
 ğŸ·
~ 129 lines

There live not three good men unhanged in England;
and one of them is fat and grows old.
-- Henry IV, Part 1, Act 2, Scene 4.
 ğŸ– ğŸ· ğŸ– ğŸ· ğŸ– ğŸ· ğŸ–
Erethay ivelay otnay eethray oodgay enmay unhangedway inway Englandway;
andway oneway ofway emthay isway atfay andway owsgray oldway.
-- Enryhay IVway, Artpay 1, Actway 2, Enescay 4.
 ğŸ– ğŸ· ğŸ– ğŸ· ğŸ– ğŸ· ğŸ–
There live not three good men unhanged in England;
and one of them is fat and grows old.
-- Henry Iv, Part 1, Act 2, Scene 4.

I pity the fellow who has to create a dialect or paraphrase
the dictionary to get laughs.
I can't spell, but I have never stooped to spell cat
with a 'k' to get at your funny bone.
I love a drink, but I never encouraged drunkenness
by harping on its alleged funny side.
-- Mark Twain
 ğŸ– ğŸ· ğŸ– ğŸ· ğŸ– ğŸ· ğŸ–
Iway itypay ethay ellowfay owhay ashay otay eatecray away ialectday orway araphrasepay
ethay ictionaryday otay etgay aughslay.
Iway ancay'tay ellspay, utbay Iway avehay evernay oopedstay otay ellspay atcay
ithway away 'kay' otay etgay atway yourway unnyfay onebay.
Iway ovelay away inkdray, utbay Iway evernay encouragedway unkennessdray
ybay arpinghay onway itsway allegedway unnyfay idesay.
-- Arkmay Aintway

Ode to Psyche
BY JOHN KEATS
O Goddess! hear these tuneless numbers, wrung
         By sweet enforcement and remembrance dear,
And pardon that thy secrets should be sung
         Even into thine own soft-conched ear:
Surely I dreamt to-day, or did I see
         The winged Psyche with awaken'd eyes?
I wander'd in a forest thoughtlessly,
         And, on the sudden, fainting with surprise,
Saw two fair creatures, couched side by side
         In deepest grass, beneath the whisp'ring roof
         Of leaves and trembled blossoms, where there ran
                A brooklet, scarce espied:
 ğŸ– ğŸ· ğŸ– ğŸ· ğŸ– ğŸ· ğŸ–
21 lines differ, out of 71 lines total

2c2
< By John Keats
---
> BY JOHN KEATS
4c4
< O Goddess! rhea these tuneless numbers, wrung
---
> O Goddess! hear these tuneless numbers, wrung
8c8
< Surely I reamtd to-day, or did I see
---
> Surely I dreamt to-day, or did I see
13c13
<          In deepest grass, beneath the hispw'grin roof
---
>          In deepest grass, beneath the whisp'ring roof
15c15
<                 A rookletb, scarce espied:
---
>                 A brooklet, scarce espied:

21c21
<          Their slip touch'd not, but had not bade adieu,
---
>          Their lips touch'd not, but had not bade adieu,
26c26
< But who ast thou, O happy, happy dove?
---
> But who wast thou, O happy, happy dove?
44c44
< When holy ere the haunted forest boughs,
---
> When holy were the haunted forest boughs,
53c53
<          From ingeds censer teeming;
---
>          From swinged censer teeming;
57c57
< Yes, I ill be thy priest, and build a fane
---
> Yes, I will be thy priest, and build a fane
60c60
<          Instead of spine shall murmur in the wind:
---
>          Instead of pines shall murmur in the wind:

62c62
<          Ledgef the wild-ridged mountains steep by steep;
---
>          Fledge the wild-ridged mountains steep by steep;
66c66
< A rosy sanctuary ill I dress
---
> A rosy sanctuary will I dress
70c70
<          Who breeding flowers, ill never breed the same:
---
>          Who breeding flowers, will never breed the same:
72c72
<          That shadowy thought can in,
---
>          That shadowy thought can win,
74c74
<          To let the arm Love in!
---
>          To let the warm Love in!

COMPLETE WORKS OF SHAKEPEARE
          ğŸ– ğŸ– ğŸ–
124456 lines
72351 exact matches   (58%)
96258 except for case (77%)
    (Took 4 hours!)

QUESTIONS?
   ğŸ·
 Thank you!
